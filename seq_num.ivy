#lang ivy1.7

include order

module seq_num = {
    type t
    instantiate total_order : totally_ordered_with_zero(t)
    instantiate iter : order_iterator(this)

    action next(seq:t) returns(res:t)

    object spec = {
	after next {
	    assume exists X.seq < X; # will not overflow in lifetime of universe
	    ensure seq < res & (X < res -> X <= seq)
	}
    }

    object impl = {
	# Each sequence number instance is interpreted as a 32-bit number
	interpret t -> bv[32]
	implement next(seq:t) returns(res:t) {
	    res := seq + 1
	}
    }
    
    isolate seq_num_iso = total_order,iter,spec,impl
}
